head	1.1;
branch	1.1.1;
access;
symbols
	asst1-end:1.1.1.1
	asst1-begin:1.1.1.1
	asst0-end:1.1.1.1
	os161-1_11:1.1.1.1
	os161:1.1.1;
locks; strict;
comment	@# @;


1.1
date	2015.09.11.01.49.07;	author xwang26;	state Exp;
branches
	1.1.1.1;
next	;

1.1.1.1
date	2015.09.11.01.49.07;	author xwang26;	state Exp;
branches;
next	;


desc
@@



1.1
log
@Initial revision
@
text
@/*
 * setjmp and longjmp for MIPS.
 */

#include <machine/asmdefs.h>

   .text
   .set noreorder

   /*
    * int setjmp(jmp_buf jb);
    *
    * Save the current state so we can return again from the call later
    * if/when longjmp is called. (If the function that called setjmp
    * returns before longjmp is called, the results are undefined. We
    * only need to save registers, not the whole contents of the stack.)
    */
   
   .globl setjmp
   .type setjmp,@@function
   .ent setjmp
setjmp:
   /*
    * jmp_buf is in a0. We need to save s0-s8, sp, and ra in it.
    * Don't store more registers without adjusting machine/setjmp.h.
    */

   sw sp, 0(a0)		/* save registers */
   sw ra, 4(a0)
   sw s0, 8(a0)
   sw s1, 12(a0)
   sw s2, 16(a0)
   sw s3, 20(a0)
   sw s4, 24(a0)
   sw s5, 28(a0)
   sw s6, 32(a0)
   sw s7, 36(a0)
   sw s8, 40(a0)

   j ra			/* done */
   li v0, 0		/* return 0 (in delay slot) */
   .end setjmp


   /*
    * void longjmp(jmp_buf jb, int code);
    */
   .globl longjmp
   .type longjmp,@@function
   .ent longjmp
longjmp:
   /*
    * jmp_buf is in a0. Return code is in a1.
    * We need to restore s0-s8, sp, and ra from the jmp_buf.
    * The return code is forced to 1 if 0 is passed in.
    */

   sltiu t0, a1, 1	/* set t0 to 1 if return code is 0... otherwise 0 */
   addu a1, a1, t0	/* update the return code */

   lw sp, 0(a0)		/* restore registers */
   lw ra, 4(a0)
   lw s0, 8(a0)
   lw s1, 12(a0)
   lw s2, 16(a0)
   lw s3, 20(a0)
   lw s4, 24(a0)
   lw s5, 28(a0)
   lw s6, 32(a0)
   lw s7, 36(a0)
   lw s8, 40(a0)

   j ra			/* return, to where setjmp was called from */
   move v0, a1		/* set return value */
   .end longjmp
@


1.1.1.1
log
@Import of os161
@
text
@@
